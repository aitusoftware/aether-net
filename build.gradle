plugins {
    id 'net.minecrell.licenser' version '0.4.1' apply false
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
}
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
def aetherVersion = file('version.txt').text.trim()
group = 'com.aitusoftware'
version = aetherVersion

defaultTasks 'clean', 'build', 'shadowJar'

ext {
    fullName = 'AetherNet'
    fullDescription = 'Aether-Net: Monitoring for Aeron'
    teamName = 'Aitu Software Limited'
    siteUrl = 'https://github.com/aitusoftware/aether-net'
    sourceUrl = 'https://github.com/aitusoftware/aether-net.git'

    javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
    if (!project.hasProperty('sonatypeUrl')) sonatypeUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    if (!project.hasProperty('sonatypeUsername')) sonatypeUsername = ''
    if (!project.hasProperty('sonatypePassword')) sonatypePassword = ''
}

def projectPom = {
    name = fullName
    description = fullDescription
    url = siteUrl
    packaging = 'pom'

    scm {
        url = "$sourceUrl"
        connection = "scm:git:https://github.com/aitusoftware.com/aether-net.git"
        developerConnection = "scm:git:https://github.com/aitusoftware.com/aether-net.git"
    }

    licenses {
        license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
        }
    }

    developers {
        developer {
            id = 'epickrram'
            name = 'Mark Price'
            email = 'aether@aitusoftware.com'
        }
    }
}

repositories {
    jcenter()
}

apply plugin: 'checkstyle'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'net.minecrell.licenser'

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '8.33'
}

license {
    header = new File(rootDir, 'config/header.txt')
    newLine = false
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar, javadocJar
}

javadoc {
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives, configurations.shadow
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: sonatypeUrl) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project(projectPom)
    }
}

uploadShadow {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project(projectPom)
        }
    }

    mustRunAfter 'uploadArchives'
}



repositories {
    jcenter()
    mavenLocal()
}

configurations {
    shadow
}

dependencies {
    compile 'io.vertx:vertx-core:3.7.0'
    compile 'com.aitusoftware:aether:0.2.1'
    compile 'io.aeron:aeron-driver:1.28.2'
    compile 'io.aeron:aeron-client:1.28.2'
    compile 'com.google.code.gson:gson:2.8.8'
    testCompile group: 'com.google.truth', name: 'truth', version: '0.42'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

mainClassName = 'com.aitusoftware.aether.net.Server'
